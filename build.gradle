//
// Building and deploying an Alexa Skill
// wolf@wolfpaulus.com
//
plugins {
    id 'java'
    id 'war'
    id "org.hidetake.ssh" version "2.7.0"
}
group 'com.techcasita.ask'
version ''

war.archiveName = 'calculator.war'

repositories {
    mavenCentral()
}

//
// Deploy the WAR file directly into an remote web container
//
ssh.settings {
    knownHosts = allowAnyHosts
    jschLog = true
}
remotes {
    alpha {
        host = 'alpha.techcasita.com'
        user = 'wolf'
        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
        agentForwarding = true
    }
}
task deploy (dependsOn: war) << {
    ssh.run {
        session(remotes.alpha) {
            put from:war.archivePath.path, into:'/usr/share/tomcat/webapps'
        }
    }
}

//
// compile and test dependencies
//

dependencies {
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    //  Alexa
    compile 'com.amazon.alexa:alexa-skills-kit:1.2'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'commons-codec:commons-codec:1.10'
    compile 'commons-io:commons-io:2.5'

    //  Alexa is currently incompatible with jackson 2.7
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.6.2'
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.2'

    //  Logger
    compile 'org.apache.logging.log4j:log4j-core:2.7'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.slf4j:slf4j-log4j12:1.7.21'

    // Tests
    testCompile 'javax.servlet:javax.servlet-api:3.1.0'
    testCompile 'junit:junit:4.12'
}
